buildscript {
    repositories {
        mavenLocal()
        maven { url "http://maven.zbsg.com.cn/content/groups/public" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE"
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
    }
}

plugins {
    id "org.sonarqube" version "2.0.1"
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
}

allprojects {
    apply plugin: 'maven'

    group = 'org.jfantasy'
    version = System.getenv()['CI_BUILD_TAG'] // Gitlab 集成时, 使用 Tag 作为版本号

    task hello << { task -> println "I'm $task.project.name" + System.getenv()['CI_BUILD_REF_NAME'] }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'spring-boot'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    sonarqube {
        //noinspection GroovyAssignabilityCheck
        properties {
            //noinspection GroovyAssignabilityCheck
            property "sonar.sourceEncoding", "UTF-8"
        }
    }

    //noinspection GroovyAssignabilityCheck
    sourceSets {
        main {
            java.srcDirs = ['JavaSource']
            resources.srcDirs = ["resources"]
        }
        test {
            java.srcDirs = ['test/src']
            resources.srcDirs = ["test/config"]
        }
    }

    compileJava.dependsOn(processResources)

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    task packageSources(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    //打包源代码
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    //如果希望gradle install，安装到.m2本地仓库，参考下面的内容
    install {
        repositories.mavenInstaller {
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = "$project.group"
        }
    }

    //noinspection GroovyAssignabilityCheck 上传到 nexus
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://maven.zbsg.com.cn/content/repositories/snapshots") {
                    authentication(userName: "admin", password: System.getenv()['MAVEN_PASSWORD'])
                }
                pom.version = "$project.version"
                pom.artifactId = "$project.name"
                pom.groupId = "$project.group"
            }
        }
    }

    artifacts.archives packageSources

    repositories {
        mavenLocal()
        maven { url "http://maven.zbsg.com.cn/content/groups/public" }
    }

}